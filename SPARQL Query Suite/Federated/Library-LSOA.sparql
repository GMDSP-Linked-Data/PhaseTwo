#Retrieve each ward from GMDSP i.e. http://gmdsp-staging.publishmydata.com/resource?uri=http%3A%2F%2Fopendatacommunities.org%2Fdoc%2Fgeography%2Fadministration%2Fwd%2FE05000687
#For each ward follow link to ordnance survey to retrieve post codes in ward i.e. http://data.ordnancesurvey.co.uk/doc/7000000000042541
#For each post code look up the post code unit on open data communities i.e. http://opendatacommunities.org/resource?uri=http%3A%2F%2Fdata.ordnancesurvey.co.uk%2Fid%2Fpostcodeunit%2FTA219HB
#Each postcode unit should have LSOA so it can be retrieved.

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX prop: <http://data.gmdsp.org.uk/data/manchester/libraries/stats/prop/>
PREFIX stats: <http://data.gmdsp.org.uk/data/manchester/libraries/stats/>
PREFIX time: <http://opendatacommunities.org/def/ontology/time/>
PREFIX year: <http://reference.data.gov.uk/id/year/>
PREFIX month: <http://reference.data.gov.uk/id/month/>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX geoO: <http://opendatacommunities.org/def/geography#>
PREFIX geoOs: <http://opendatacommunities.org/def/ontology/geography/>
PREFIX post: <http://data.ordnancesurvey.co.uk/ontology/postcode/>
PREFIX dep: <http://opendatacommunities.org/def/ontology/societal-wellbeing/deprivation/>
SELECT DISTINCT ?count ?label ?ordRef ?pcu ?lsoa ?score
WHERE {
	{
		SELECT DISTINCT ?count ?ordRef ?label
		WHERE {
			?obv rdf:type qb:Observation ;
				qb:dataSet stats:loans ;
				prop:period month:2014-12 ;
				prop:area ?ward ;
				prop:count ?count .
			?ward owl:sameAs ?ordRef ;
				rdfs:label ?label .
		}
		ORDER BY DESC (?count)
		LIMIT 3
	}
	SERVICE <http://data.ordnancesurvey.co.uk/datasets/os-linked-data/apis/sparql> {
		?pcu rdf:type post:PostcodeUnit ;
			post:ward ?ordRef . 
	}
	SERVICE <http://opendatacommunities.org/sparql>{
		?pcu geoO:lsoa ?lsoa .
		?obv time:refPeriod year:2010 ;
			geoOs:refArea ?lsoa ;
			dep:imdScore ?score .
	}
}